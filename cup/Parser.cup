package Compi;

import java_cup.runtime.*;
import java.util.LinkedList;


parser code {:

:}

terminal Boolean TRUE, FALSE;
terminal float REAL;
terminal String SEMICOLON, COLON, PAR_C, PAR_A, MUSTACHE_A, MUSTACHE_C, CORCH_A, CORCH_C, ASIG, SUMASIG, RESASIG, IGUAL, SUM, RES, MENOR, MAYOR, MUL, DIV, PORCENT, MAY_IGUAL, MEN_IGUAL, AND, OR, NO_IGUAL, NEGAC, ID, BOOL, BREAK, CLASS, CONTINUE, ELSE, FLOAT, FOR, IF, INTEGER, RETURN, VOID, WHILE, EXTERN, PUNTO;
terminal Integer NUM;

non terminal Program program;
non terminal Class_decl class_decl;
non terminal LinkedList<Field_decl> field_declarations;
non terminal Field_decl field_decl;
non terminal LinkedList<Method_decl> method_declarations;
non terminal Method_decl method_decl;
non terminal Body body;
non terminal Block block;
non terminal LinkedList<Statement> bl_statement;
non terminal Type type;
non terminal Statement statement;
non terminal Asign_op asign_op;
non terminal Expr expr;
non terminal LinkedList<Param_decl> param_declarations;
non terminal Param_decl param_decl;
non terminal LinkedList<Expr> param_expr;
non terminal Compi.Location location;
non terminal Method_call method_call;
non terminal Method_call_expr method_call_expr;
non terminal LinkedList<Name> list_names; 
non terminal Name name ;

precedence left OR, AND;
precedence nonassoc  MENOR, MAYOR, MEN_IGUAL, MAY_IGUAL, IGUAL, NO_IGUAL;
precedence left SUM, RES;
precedence left MUL, DIV, PORCENT;
precedence left ELSE, NEGAC;
 
program ::= class_decl:c {: RESULT = new Program(c); :}
         | program:p class_decl:c {: p.addClass(c); RESULT=p; :} 
         ;

class_decl ::= CLASS:cl ID:name MUSTACHE_A field_declarations:f method_declarations:m MUSTACHE_C {: RESULT = new Class_decl(name,f,m,clxleft.getLine()); :}
		    |CLASS:cl ID:name MUSTACHE_A  method_declarations:m MUSTACHE_C {: RESULT = new Class_decl(name,clxleft.getLine(),m); :}
		    |CLASS:cl ID:name MUSTACHE_A field_declarations:f MUSTACHE_C {: RESULT = new Class_decl(name,f,clxleft.getLine()); :}
		    |CLASS:cl ID:name MUSTACHE_A  MUSTACHE_C {: RESULT = new Class_decl(name,clxleft.getLine()); :}
		    ;
		
field_declarations ::= field_decl:f {: LinkedList<Field_decl> fd = new LinkedList<Field_decl>();
										fd.add(f); RESULT = fd; :} 
					| field_declarations:fd field_decl:f {: fd.add(f); RESULT = fd; :}
			        ;

field_decl ::=  type:t	list_names:l SEMICOLON {: RESULT = new Field_decl(t, l, txleft.getLine()); :}
			;

list_names ::= name:n {: LinkedList<Name> ln = new LinkedList<Name>(); ln.add(n); RESULT = ln; :}
			| list_names:ln COLON name:n {: ln.add(n); RESULT = ln; :}
			;

name ::= ID:id {: RESULT = new Name(id, idxleft.getLine()); :}
		| ID:id CORCH_A NUM:il CORCH_C {: RESULT = new Name(id, il, idxleft.getLine()); :}
		;

method_declarations ::= method_decl:m {: LinkedList<Method_decl> md = new LinkedList<Method_decl>();
										md.add(m); RESULT = md; :} 
					| method_declarations:md method_decl:m {: md.add(m); RESULT = md; :}
					;

method_decl ::= type:t ID:id PAR_A PAR_C body:b {: RESULT = new Method_decl(t, id, b, idxleft.getLine()); :}
			 | type:t ID:id PAR_A param_declarations:pd PAR_C body:b {: RESULT = new Method_decl(t, id, pd, b, idxleft.getLine()); :}
			 ;

param_declarations ::= param_decl:p {: LinkedList<Param_decl> pd = new LinkedList<Param_decl>();
										pd.add(p); RESULT = pd; :}
	     | param_declarations:pd COLON param_decl:p {: pd.add(p); RESULT = pd; :}
	     ;

param_decl ::= type:t ID:id {: RESULT = new Param_decl(id, t, txleft.getLine()); :}
			;

body ::= block:b {: RESULT = new Body(b); :}
	| EXTERN:e SEMICOLON {: RESULT = new Body(exleft.getLine()); :} 
	;

block ::= MUSTACHE_A field_declarations:f bl_statement:s MUSTACHE_C {: RESULT = new Block(f, s, fxleft.getLine()); :}
	| MUSTACHE_A field_declarations:f MUSTACHE_C {: RESULT = new Block(f, fxleft.getLine()); :}
	| MUSTACHE_A bl_statement:s MUSTACHE_C {: RESULT = new Block(sxleft.getLine(), s); :}
	| MUSTACHE_A:m MUSTACHE_C {: RESULT = new Block(mxleft.getLine()); :}
	;


bl_statement ::= statement:s {: LinkedList<Statement> ls = new LinkedList<Statement>(); ls.add(s); RESULT = ls; :}
	| bl_statement:ls statement:s {: ls.add(s); RESULT = ls; :}
	;	

type ::= INTEGER {: RESULT = new Type("integer"); :}
	|FLOAT {: RESULT = new Type("float"); :}
	|BOOL {: RESULT = new Type("bool"); :}
	|ID:t {: RESULT = new Type(t); :}
	|VOID {: RESULT = new Type("void"); :}
	;

statement ::= location:l asign_op:a expr:e SEMICOLON {: RESULT = new Statement_asig(l, a, e); :}
	  |method_call:m SEMICOLON {: RESULT = m; :}
	  |IF:i PAR_A expr:e PAR_C statement:s {: RESULT = new Statement_if(e, s, ixleft.getLine()); :} 
	  |IF:i PAR_A expr:e PAR_C statement:s ELSE statement:st {: RESULT = new Statement_ifelse(e, s, st, ixleft.getLine()); :}
	  |FOR:f ID:id ASIG expr:e COLON expr:ex statement:s {: RESULT = new Statement_for(id, e, ex, s, fxleft.getLine()); :}
	  |WHILE:w expr:e statement:s {: RESULT = new Statement_while(e, s, wxleft.getLine()); :}
	  |RETURN:r expr:e SEMICOLON {: RESULT = new Statement_expr(e, rxleft.getLine()); :}
	  |RETURN:r SEMICOLON {: RESULT = new Statement_void(rxleft.getLine()); :}
	  |BREAK:b SEMICOLON {: RESULT = new Statement_break(bxleft.getLine()); :}
	  |CONTINUE:c SEMICOLON {: RESULT = new Statement_continue(cxleft.getLine()); :}
	  |SEMICOLON:s {: RESULT = new Statement_semicolon(sxleft.getLine()); :}
	  |block:b {: RESULT = b; :}
	  ;

asign_op ::= ASIG {: RESULT = new Asign_op("="); :}
	  |SUMASIG {: RESULT = new Asign_op("+="); :}
	  |RESASIG {: RESULT = new Asign_op("-="); :}
	  ;

method_call ::= ID:id PAR_A param_expr:pe PAR_C {: RESULT = new Method_call(id, idxleft.getLine(), pe); :}
		|ID:id PAR_A PAR_C {: RESULT = new Method_call(id, idxleft.getLine()); :}
		|ID:id PUNTO ID:pi PAR_A param_expr:pe PAR_C {: RESULT = new Method_call(id, pe, pi, idxleft.getLine()); :}
		|ID:id PUNTO ID:pi PAR_A PAR_C {: RESULT = new Method_call(id, pi, idxleft.getLine()); :} 
		;

param_expr ::= expr:e {: LinkedList<Expr> ex = new LinkedList<Expr>(); ex.add(e); RESULT = ex; :}
	    | param_expr:ex COLON expr:e {: ex.add(e); RESULT = ex; :}
	    ;

location ::= ID:id {: RESULT = new Compi.Location(id, idxleft.getLine()); :}
	|ID:id PUNTO ID:pi {: RESULT = new Compi.Location(id, pi, idxleft.getLine()); :}
	|ID:id CORCH_A expr:e CORCH_C {: RESULT = new Compi.Location(id, e, idxleft.getLine()); :}
	|ID:id PUNTO ID:pi CORCH_A expr:e CORCH_C {: RESULT = new Compi.Location(id, pi, e, idxleft.getLine()); :}
	;

method_call_expr ::= ID:id PAR_A param_expr:pe PAR_C {: RESULT = new Method_call_expr(id, idxleft.getLine(), pe); :}
		|ID:id PAR_A PAR_C {: RESULT = new Method_call_expr(id, idxleft.getLine()); :}
		|ID:id PUNTO ID:pi PAR_A param_expr:pe PAR_C {: RESULT = new Method_call_expr(id, pe, pi, idxleft.getLine()); :}
		|ID:id PUNTO ID:pi PAR_A PAR_C {: RESULT = new Method_call_expr(id, pi, idxleft.getLine()); :} 
		;

expr ::= location:l {: RESULT = l; :}
	| method_call_expr:m {: RESULT = m; :}
	| RES:o expr:e1 {: RESULT = new Unary_op("-",e1,oxleft.getLine());:}
	| NEGAC:o expr:e1 {: RESULT = new Unary_op("!",e1,oxleft.getLine());:}
	| PAR_A expr:e PAR_C {: RESULT = e; :}
	| expr:e1 SUM:o expr:e2 {: RESULT = new Bin_op(e1, "+", e2, oxleft.getLine()); :}
	| expr:e1 RES:o expr:e2 {: RESULT = new Bin_op(e1, "-", e2, oxleft.getLine()); :}
	| expr:e1 MUL:o expr:e2 {: RESULT = new Bin_op(e1, "*", e2, oxleft.getLine()); :}
	| expr:e1 DIV:o expr:e2 {: RESULT = new Bin_op(e1, "/", e2, oxleft.getLine()); :}
	| expr:e1 PORCENT:o expr:e2 {: RESULT = new Bin_op(e1, "%", e2, oxleft.getLine()); :}
	| expr:e1 MENOR:o expr:e2 {: RESULT = new Bin_op(e1, "<", e2, oxleft.getLine()); :}
	| expr:e1 MAYOR:o expr:e2 {: RESULT = new Bin_op(e1, ">", e2, oxleft.getLine()); :}
	| expr:e1 MAY_IGUAL:o expr:e2 {: RESULT = new Bin_op(e1, ">=", e2, oxleft.getLine()); :}
	| expr:e1 MEN_IGUAL:o expr:e2 {: RESULT = new Bin_op(e1, "<=", e2, oxleft.getLine()); :}
	| expr:e1 IGUAL:o expr:e2 {: RESULT = new Bin_op(e1, "==", e2, oxleft.getLine()); :}
	| expr:e1 NO_IGUAL:o expr:e2 {: RESULT = new Bin_op(e1, "!=", e2, oxleft.getLine()); :}
	| expr:e1 AND:o expr:e2 {: RESULT = new Bin_op(e1, "&&", e2, oxleft.getLine()); :}
	| expr:e1 OR:o expr:e2 {: RESULT = new Bin_op(e1, "||", e2, oxleft.getLine()); :}
	| TRUE {: RESULT = new Literal_boolean(true); :}
	| FALSE {: RESULT = new Literal_boolean(false); :}
	| REAL:x {: RESULT = new Literal_float(x); :}
	| NUM:x {: RESULT = new Literal_integer(x); :}
	;




